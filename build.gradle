buildscript {
  ext.versions = [
      'minSdk': 14,
      'compileSdk': 31,
      'kotlin': '1.6.0',
      'lint': '30.0.0'
  ]

  repositories {
    mavenCentral()
    google()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:7.0.3'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    classpath 'com.google.dagger:hilt-android-gradle-plugin:2.38.1'
  }
}

allprojects {
  repositories {
    mavenCentral()
    google()
  }

  group = GROUP
  version = VERSION_NAME
}

ext {
  // Android dependencies.
  supportV4 = 'com.android.support:support-v4:27.0.0'
  supportAnnotations = 'com.android.support:support-annotations:27.0.0'
  supportTestRunner = 'com.android.support.test:runner:0.5'
  fragmentKtx = 'androidx.fragment:fragment-ktx:1.4.0'

  supportSqlite = 'android.arch.persistence:db:1.0.0'
  supportSqliteFramework = 'android.arch.persistence:db-framework:1.0.0'

  // Third-party dependencies.
  kotlinStdLib = "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"
  kotlinCoroutines = "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2"
  viewModel = "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0"
  hilt = "com.google.dagger:hilt-android:2.38.1"
  hiltCompiler = "com.google.dagger:hilt-compiler:2.38.1"
  dagger = 'com.google.dagger:dagger:2.13'
  daggerCompiler = 'com.google.dagger:dagger-compiler:2.13'
  timber = 'com.jakewharton.timber:timber:4.6.0'
  autoValue = 'com.google.auto.value:auto-value:1.5'
  autoValueParcel = 'com.ryanharter.auto.value:auto-value-parcel:0.2.9'
  junit = 'junit:junit:4.12'
  truth = 'com.google.truth:truth:0.36'

  // Lint dependencies.
  lintApi = "com.android.tools.lint:lint-api:${versions.lint}"
  lint = "com.android.tools.lint:lint:${versions.lint}"
  lintTests = "com.android.tools.lint:lint-tests:${versions.lint}"
}

configurations {
  osstrich
}
dependencies {
  osstrich 'com.squareup.osstrich:osstrich:1.2.0'
}
task publishV1Javadoc(type: JavaExec) {
  classpath = configurations.osstrich
  main = 'com.squareup.osstrich.JavadocPublisher'
  args = [
      'build/javadoc',
      'https://github.com/square/sqlbrite',
      'com.squareup.sqlbrite'
  ]
}
task publishV2Javadoc(type: JavaExec) {
  classpath = configurations.osstrich
  main = 'com.squareup.osstrich.JavadocPublisher'
  args = [
      'build/javadoc',
      'https://github.com/square/sqlbrite',
      'com.squareup.sqlbrite2'
  ]
}
task publishV3Javadoc(type: JavaExec) {
  classpath = configurations.osstrich
  main = 'com.squareup.osstrich.JavadocPublisher'
  args = [
      'build/javadoc',
      'https://github.com/square/sqlbrite',
      'com.squareup.sqlbrite3'
  ]
}
task publishJavadoc(dependsOn: [publishV1Javadoc, publishV2Javadoc, publishV3Javadoc])
